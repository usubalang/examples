set terminal lua tikz standalone
set output 'slicing-compare.tex'

set style data histogram
set style histogram cluster gap 1
set style fill solid border rgb "black"

set border 3
set tics nomirror
set grid ytics

set xrange [-0.7:3.5]

set ylabel "cycles/byte"
set xlabel "~"

# set key font ",14"
# set xtics font ",14" 
# set ytics font ",14"

set linetype 1 lc rgb '#0000FF'
set linetype 2 lc rgb '#FF0000'


#set boxwidth 0.9
#set xtic scale 0

# 2, 3, 4, 5 are the indexes of the columns; 'fc' stands for 'fillcolor'
plot 'data.dat' using 2:xtic(1) ti col fs pattern 3 lt 2, \
     '' u 3 ti col fs pattern 1 lt 1, \
     '' u 4 ti col fs pattern 5 lt 1, \
     '' u 5 ti col fs pattern 6 lt 1, \
     '' u ($0-1.3):($2+1):2 with labels notitle, \
     '' u ($0-1.1):($3+1):3 with labels notitle, \
     '' u ($0-0.9):($4+1):4 with labels notitle, \
     '' u ($0-0.7):($5+1):5 with labels notitle

set output
system('pdflatex slicing-compare.tex')
